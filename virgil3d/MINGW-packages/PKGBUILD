# Courtesy of qemu-3dfx <liewkj@yahoo.com>

_realname=virglrenderer
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=1.0.0
pkgrel=1
pkgdesc='A virtual 3D GPU library, that allows the guest operating system to use the host GPU to accelerate 3D rendering'
arch=('any')
mingw_arch=('mingw64')
url='https://virgil3d.github.io/'
license=(MIT)
depends=("${MINGW_PACKAGE_PREFIX}-libepoxy")
makedepends=("${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-ninja")
_tag=$pkgver
source=("virglrenderer-$pkgver.tar.bz2::https://gitlab.freedesktop.org/virgl/virglrenderer/-/archive/$_tag/virglrenderer-$_tag.tar.bz2"
        "0001-Virglrenderer-on-Windows-and-macOS.patch")
sha256sums=('28c18f3f631eb804c71572abe685bd1ee14e326998a669152ef32f422d1c11d8'
            '90367a5caf50f0a0ff97fda409994593b66fd923a4b0f77bd7a04eebc9193eed')

prepare() {
  cd virglrenderer-$_tag
  sed "s/\(error=switch\)/\1\',\'\-Wno\-unknown\-attributes\',\'\-Wno\-unused\-parameter/" -i meson.build
  sed "s/\(fvisibility=hidden\)/\1\',\'\-mno\-ms\-bitfields/" -i meson.build
  patch -p0 -i ${srcdir}/0001-Virglrenderer-on-Windows-and-macOS.patch
}

build() {
  cd virglrenderer-$_tag
  MSYS2_ARG_CONV_EXCL="--prefix" \
  meson --prefix="${MINGW_PREFIX}" build # -Dtests=true
  ninja -C build
}

package() {
  cd virglrenderer-$_tag
  DESTDIR="$pkgdir" ninja -C build install
  install -D -m644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}/${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
